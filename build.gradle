plugins {
    id 'java'
    id 'org.hidetake.ssh' version '2.11.2'
}

group = 'com.mayer99'
version '1.0.0'

repositories {
    mavenCentral()
}

remotes {
    pi {
        host = System.getenv("RASPBERRYPI_URL")
        user = System.getenv("RASPBERRYPI_USER")
        password = System.getenv("RASPBERRYPI_PASSWORD")
    }

}

tasks.register('deploy', Jar) {
    group = "remote"
    manifest {
        attributes (
                "Main-Class": "com.mayer99.narcotrack.application.NarcotrackApplication"
        )
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
    doLast {
        ssh.run {
            session(remotes.pi) {
                put from: file("$buildDir/libs/Narcotrack-1.0.0.jar"), into: '/home/pi/app'
            }

        }
    }
}

tasks.register('deploySlimJar', Jar) {
    group = "remote"
    manifest {
        attributes (
                "Main-Class": "com.mayer99.narcotrack.application.NarcotrackApplication",
                "Class-Path": configurations.runtimeClasspath.collect {"lib/$it.name" }.join(' ')
        )
    }
    with jar
    doLast {
        ssh.run {
            session(remotes.pi) {
                put from: file("$buildDir/libs/Narcotrack-1.0.0.jar"), into: '/home/pi/app'
            }

        }
    }
}

tasks.register('execute') {
    group = "remote"
    doLast {
        ssh.run {
            session(remotes.pi) {
                execute 'sudo java -jar /home/pi/app/Narcotrack-1.0.0.jar'
            }

        }
    }

}

tasks.register('deployDependencies') {
    group = "remote"
    doLast {
        ssh.run {
            session(remotes.pi) {
                put from: configurations.runtimeClasspath, into: '/home/pi/app/lib'
            }

        }
    }
}

dependencies {
    implementation 'org.mariadb.jdbc:mariadb-java-client:3.1.2'
    implementation 'com.fazecast:jSerialComm:2.9.3'
    implementation 'ch.qos.logback:logback-core:1.4.14'
    implementation 'org.slf4j:slf4j-parent:2.0.5'
    implementation 'ch.qos.logback:logback-classic:1.4.12'
    implementation 'org.json:json:20240303'
}